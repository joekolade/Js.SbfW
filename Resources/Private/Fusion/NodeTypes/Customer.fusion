prototype(Joekolade.Bureau:Customer.Document) < prototype(Neos.NodeTypes:Internal.Document) {
    body {
        # customer content
        content >
        content = Neos.NodeTypes:Page.Document {
            head >
            httpResponseHead >
            body {
                content >
                templatePath = 'resource://Joekolade.Bureau/Private/Templates/NodeTypes/Customer.html'
                sectionName = 'body'

                title = ${q(node).property('title')}

                customerurl = Joekolade.Bureau:Customerurl {
                    customer = ${q(node)}
                }

                customercms = Joekolade.Bureau:Customercms {
                    customer = ${q(node)}
                }

                customerhoster = Joekolade.Bureau:Customerhoster {
                    customer = ${q(node)}
                }

                gdpr = ${q(node).property('gdpr')}

                recommendation = ${q(node).property('recommendation')}
                comment = ${q(node).property('comment')}

                main = Neos.Neos:PrimaryContent {
                    nodePath = 'main'
                }

                references = Neos.Neos:ContentCollection {
                    nodePath = 'references'
                }

                tasks = Neos.Neos:ContentCollection {
                    nodePath = 'tasks'
                }

                agency = ${q(node).property('agency')}
            }
        }
    }
}

prototype(Joekolade.Bureau:Customerurl) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://Joekolade.Bureau/Private/Templates/NodeTypes/Partials/CustomerUrl.html'

    domain = ${q(this.customer).property('domain')}
    cms-adminurl = ${q(q(this.customer).property('cms')).property('adminurl')}
}

prototype(Joekolade.Bureau:Customercms) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://Joekolade.Bureau/Private/Templates/NodeTypes/Partials/CustomerCms.html'

    cms = ${q(this.customer).property('cms')}
    cms-title = ${q(q(this.customer).property('cms')).property('title')}
    cms-icon = ${q(q(this.customer).property('cms')).property('icon')}
    cms-releasenotes = ${q(q(this.customer).property('cms')).property('releasenotes')}
    cms-patched = ${Date.now() < q(q(this.customer).property('cms')).property('patcheduntil') ? 'true' : 'false'}
    version = ${q(this.customer).property('version')}
}

prototype(Joekolade.Bureau:Customerhoster) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://Joekolade.Bureau/Private/Templates/NodeTypes/Partials/CustomerHoster.html'

    hoster = ${q(this.customer).property('hoster')}
    hoster-title = ${q(q(this.customer).property('hoster')).property('title')}
    hoster-url = ${q(q(this.customer).property('hoster')).property('url')}
    hoster-controlpanel = ${q(q(this.customer).property('hoster')).property('controlpanel')}
}

// Deprecated!
prototype(Joekolade.Bureau:ChildCustomers) < prototype(Neos.Fusion:Collection) {
    @process.wrap = ${'<ul>' + value + '</ul>'}
    collection = ${q(node).find('[instanceof Joekolade.Bureau:Customer]')}
    itemName = 'customer'
    itemRenderer = Neos.Fusion:Template {
        templatePath = 'resource://Joekolade.Bureau/Private/Templates/FusionObjects/AgencyCustomerListitem.html'
        customer = ${customer}
        title = ${q(customer).property('title')}
        domain = ${q(customer).property('domain')}
    }
}