/**
 * Root Fusion template for the SchÃ¤fer Webentwicklung site
 */

## Include all .fusion files in NodeTypes
include: NodeTypes/*

root {
    default {
        type = ${q(node).property('_nodeType') + '.Document'}
        renderPath >
    }
}

prototype(Joekolade.Bureau:InternalPartial) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Partials/InternalPartials.html'
    sectionName = 'default'
}

prototype(Neos.NodeTypes:Page.Document) >
prototype(Neos.NodeTypes:Page.Document) < prototype(Neos.Neos:Page) {

    head {
        stylesheets.site = Neos.Fusion:Template {
            templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Default.html'
            sectionName = 'stylesheets'
        }

        javascripts.site = Neos.Fusion:Template {
            templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Default.html'
            sectionName = 'headScripts'
        }
    }

    bodyTag.attributes.class = 'public'
    body {
        templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Default.html'
        sectionName = 'body'
        parts {

            menu = Neos.Neos:Menu

            header = Joekolade.Bureau:InternalPartial {
                sectionName = 'header'
            }

            credentials = Joekolade.Bureau:InternalPartial {
                sectionName = 'credentials'
                // TODO: credentials
                agency = ${q(node).parents().filter('[instanceof Joekolade.Bureau:Agency]').get(0)}
                title = ${q(node).parents().filter('[instanceof Joekolade.Bureau:Agency]').first().property('title')}
            }
        }

        // These are your content areas, you can define as many as you want, just name them and the nodePath.
        content {
            // The default content section
            main = Neos.Neos:PrimaryContent {
                nodePath = 'main'
            }
        }

        javascripts.site = Neos.Fusion:Template {
            templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Default.html'
            sectionName = 'bodyScripts'
        }
    }
}

prototype(Neos.NodeTypes:Internal.Document) < prototype(Neos.NodeTypes:Page.Document) {
    head {
        stylesheets.site = Neos.Fusion:Template {
            templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Internal.html'
            sectionName = 'stylesheets'
        }

        javascripts.site = Neos.Fusion:Template {
            templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Internal.html'
            sectionName = 'headScripts'
        }
    }

    body {
        templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Internal.html'
        sectionName = 'body'
        parts {
            menu = Neos.Neos:Menu
        }

        // These are your content areas, you can define as many as you want, just name them and the nodePath.
        content >

        javascripts.site = Neos.Fusion:Template {
            templatePath = 'resource://Joekolade.Bureau/Private/Templates/Page/Internal.html'
            sectionName = 'bodyScripts'
        }
    }
}

